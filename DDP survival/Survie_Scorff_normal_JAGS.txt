# Modele Scorff

model
{
#Definition des priors
a ~ dbeta(1,1)
Rmax ~ dunif(0,100)

#Modélisation de la taille de l'échantillonnage (=precision) pour la loi beta sur la survie
n ~ dunif(0,1000)

for (t in 1:year)
{

Fsw[t] ~ dbeta(1,1)
FSW[t] ~ dbeta(1,1)

### Formalisation de la vraisemblance sur les captures
Ysw[t] ~ dbin (Fsw[t],sw[t])
YSW[t] ~ dbin (FSW[t],SW[t])


EGG[t] <- (SW[t]*PROPFEMSW*NBEGGSW + sw[t]*PROPFEMsw*NBEGGsw)/SRR

##Relation de B-H sur la survie

alpha[t] <- n/((1/a)+(EGG[t]/Rmax))
beta[t] <- n-alpha[t]
theta[t] ~ dbeta(alpha[t],beta[t])

##Modelisation du recrutement

muPARR[t] <-  theta[t]*EGG[t]
tauPARR[t] <- SRR/(muPARR[t])
PARR[t] ~ dnorm(muPARR[t],tauPARR[t])

# Erreurs normalisees 
lg.e[t] <- log(PARR[t]/((alpha[t]/n)*EGG[t]))

#Predictions des quantiles
thetaquant[t] ~ dbeta(alpha[t],beta[t])
}

#Predictions pour differentes valeurs de S : c(seq(0,1000,10),2000)
for (i in 1:101) 
{
alphapred[i] <- n/((1/a)+((i-1)*10/Rmax))
betapred[i] <- n-alphapred[i] 
thetapred[i] ~ dbeta(alphapred[i],betapred[i])

muPARRpred[i] <-  thetapred[i]*(i-1)*10+10^(-9)
tauPARRpred[i] <- SRR/(muPARRpred[i])
PARRpred[i] ~ dnorm(muPARRpred[i],tauPARRpred[i])
}
alphapred[102] <- n/((1/a)+(2000/Rmax))
betapred[102] <- n-alphapred[102] 
thetapred[102] ~ dbeta(alphapred[102],betapred[102])

muPARRpred[102] <-  thetapred[102]*2000
tauPARRpred[102] <- SRR/(muPARRpred[102])
PARRpred[102] ~ dnorm(muPARRpred[102],tauPARRpred[102])
}












